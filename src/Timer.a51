#include <REG51F380.H>

PUBLIC LOAD_ARRAY_TIMER_BACKWARD
PUBLIC INCREMENTA_TIMER
PUBLIC RELOAD_TIMER

EXTRN NUMBER(VTIMER_BYTE_LEN)

CSEG AT 2300H
;\\\\\\\\\\\\\\\\\\\ROTINAS AUXILIARES MAQUINA ESTADOS SECUNDÁRIA///////////////////
INCREMENTA_TIMER:
;//////////////////////////////////
; RECEBE VTIMER ARRAY POR R0
; RECEBE TIMER INDEX POR R7
; USA R7 PARA DEVOLVER O OVERFLOW
;//////////////////////////////////
USING 0
	PUSH ACC
	PUSH AR0
	MOV A,R7
	RL A
	RL A
	ADD A,R0
	MOV R0,A
	CLR C
	MOV A,@R0
	ADD A,#1
	MOV @R0,A
	MOV R7, #(VTIMER_BYTE_LEN-1)
INC_LOOP:
	INC R0
	MOV A,@R0
	ADDC A,#0
	MOV @R0,A
	DJNZ R7,INC_LOOP
	CLR A
	RLC A
	MOV R7,A
	POP AR0
	POP ACC
	RET
RELOAD_TIMER:
;//////////////////////////////////
; RECEBE ARRAY RELOAD POR DPTR
; RECEBE VTIMER ARRAY POR R0
; RECEBE TIMER INDEX POR R7
;//////////////////////////////////
USING 0
	PUSH ACC
	PUSH AR0
	PUSH AR5
	MOV A,R7
	RL A
	RL A
	MOV R5,A
	ADD A,R0
	MOV R0,A
	MOV R7,#VTIMER_BYTE_LEN
R_LOOP:
	MOV A,R5
	MOVC A,@A+DPTR
	MOV @R0,A
	INC R5 
	INC R0
	DJNZ R7,R_LOOP
	POP AR5
	POP AR0
	POP ACC
	RET

LOAD_ARRAY_TIMER_BACKWARD:
;//////////////////////////////////
; RECEBE ARRAY RELOAD POR DPTR
; RECEBE VTIMER ARRAY POR R0
; RECEBE RELOAD LEN POR R7
;//////////////////////////////////
	PUSH ACC
	MOV A,R0
	ADD A,R7
	DEC A
	MOV R0,A
	MOV A,R7
BLOAD_LOOP:
	DEC A
	MOV R7,A
	MOVC A,@A+DPTR
	MOV @R0,A
	DEC R0
	MOV A,R7
	JNZ BLOAD_LOOP
	POP ACC
	RET
END